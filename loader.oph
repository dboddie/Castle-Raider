; Copyright (C) 2011 David Boddie <david@boddie.org.uk>
;
; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.org $3500

.include "constants.oph"

main:
    lda #22         ; MODE 5
    jsr $ffee
    lda #5
    jsr $ffee

    lda #23         ; disable flashing cursor
    jsr $ffee
    lda #1
    jsr $ffee
    ldx #7
    cursor_loop:
        lda #0
        jsr $ffee
        dex
        bpl cursor_loop

    jsr set_core_palette

    ldx #0
    title_text_loop:
        lda title_text,x
        jsr $ffee
        inx
        cpx #[title_text_end - title_text]
        bne title_text_loop

    jsr draw_title

    jsr init_update_title

    lda #0                  ; clear polling flag to indicate that the animation
    sta $8f                 ; is not running as an interrupt routine

    title_animation_loop:

        jsr update_title

        ldx #157            ; (SPACE)
        jsr check_key
        cpy #255
        bne title_animation_loop

    clc

    lda #1                  ; set polling flag to ensure that the interrupt
    sta $8f                 ; routine gives enough time to the OS

    jsr cassette_init

    ldx #0
    init_load_window_loop:

        lda init_load_window_vdu_bytes,x
        jsr $ffee
        inx
        cpx #5
        bne init_load_window_loop

    lda #255
    ldx #<tiles_block
    ldy #>tiles_block
    jsr $ffdd

    lda #255
    ldx #<sprites_block
    ldy #>sprites_block
    jsr $ffdd

    lda #255
    ldx #<levels_block
    ldy #>levels_block
    jsr $ffdd

    lda #255
    ldx #<top_panel_block
    ldy #>top_panel_block
    jsr $ffdd

    lda #255
    ldx #<code_block
    ldy #>code_block
    jsr $ffdd

    jsr cassette_quit

    lda #140
    jsr $fff4   ; *TAPE (reclaim any workspace used on high PAGE systems)

    ; Copy the panel onto bank1.
    jsr copy_panel

    jmp code_start_address

set_core_palette:

    lda #2
    sta $70
    lda #2
    sta $71
    jsr set_palette

    lda #3
    sta $70
    lda #3
    sta $71
    jsr set_palette

    rts

set_palette:
                    ; $70=logical colour
                    ; $71=physical colour
    lda $70
    sta $3dfb
    lda $71
    sta $3dfc
    lda #0
    sta $3dfd
    sta $3dfe
    sta $3dff

    lda #$c         
    ldx #$fb
    ldy #$3d
    jsr $fff1
    rts

copy_panel:

    lda #$00
    sta $70
    lda #$30
    sta $71
    lda #$00
    sta $72
    lda #$58
    sta $73

    ldy #0

    copy_top_panel_loop:

        lda ($70),y
        sta ($72),y

        lda $70
        adc #1
        sta $70
        lda $71
        adc #0
        sta $71
        clc

        lda $72
        adc #1
        sta $72
        lda $73
        adc #0
        sta $73
        clc

        cmp #$5d
        bne copy_top_panel_loop

    clc
    rts

check_key:      ; x=key code
    lda #129    ; returns y=255 or 0
    ldy #255
    jsr $fff4
    rts

tiles_block: .byte <tiles_file_name, >tiles_file_name
             .byte sprite_area_low, sprite_area_high, 0, 0
             .byte sprite_area_low, sprite_area_high, 0, 0
             .byte sprite_area_length_low, sprite_area_length_high, 0, 0
             .byte sprite_area_end_low, sprite_area_end_high, 0, 0

tiles_file_name: .byte "TILES", 13

sprites_block: .byte <sprites_file_name, >sprites_file_name
             .byte char_area_low, char_area_high, 0, 0
             .byte char_area_low, char_area_high, 0, 0
             .byte char_area_length_low, char_area_length_high, 0, 0
             .byte char_area_end_low, char_area_end_high, 0, 0

sprites_file_name: .byte "SPRITES", 13

levels_block: .byte <levels_file_name, >levels_file_name
             .byte levels_address_low, levels_address_high, 0, 0
             .byte levels_address_low, levels_address_high, 0, 0
             .byte levels_length_low, levels_length_high, 0, 0
             .byte levels_end_low, levels_end_high, 0, 0

levels_file_name: .byte "LEVELS", 13

top_panel_block: .byte <top_panel_file_name, >top_panel_file_name
             .byte top_panel_address_low, top_panel_address_high, 0, 0
             .byte top_panel_address_low, top_panel_address_high, 0, 0
             .byte top_panel_length_low, top_panel_length_high, 0, 0
             .byte top_panel_end_low, top_panel_end_high, 0, 0

top_panel_file_name: .byte "PANEL", 13

code_block: .byte <code_file_name, >code_file_name
             .byte code_start_low, code_start_high, 0, 0
             .byte code_start_low, code_start_high, 0, 0
             .byte code_length_low, code_length_high, 0, 0
             .byte code_end_low, code_end_high, 0, 0

code_file_name: .byte "CODE", 13

init_load_window_vdu_bytes: .byte 28,0,28,19,26

title_text: .byte "   Castle Raider", 13, 10
            .byte "Pre-release  version"
            .byte " Copyright (C) 2013", 13, 10
            .byte "    David Boddie", 13, 10
            .byte "GNU GPL v3 or later"
title_text_end:

draw_title:

    lda #7
    sta $74

    draw_title_outer_loop:

        lda $74
        cmp #4
        bcc draw_title_outer_loop_upper

        lda #$c4
        sta $72
        lda #$60
        sta $73

        jmp draw_title_outer_loop_columns

        draw_title_outer_loop_upper:

        lda #$8c
        sta $72
        lda #$5f
        sta $73

        draw_title_outer_loop_columns:

        lda #0
        sta $84                     ; column counter

        ldx #0
        draw_title_loop:

            jsr draw_column

            sec
            lda $72
            sbc #$f8
            sta $72
            lda $73
            sbc #$13
            sta $73
            clc

            lda $83                         ; if we encounter a 0xff padding byte
            cmp #0                          ; then increment X to skip past it
            beq draw_title_loop_no_increment

            inx

            draw_title_loop_no_increment:

            inc $84
            lda $84
            cmp #38
            beq draw_title_loop_exit
            jmp draw_title_loop

        draw_title_loop_exit:

        dec $74
        bpl draw_title_outer_loop

    draw_title_exit:
    clc
    rts

draw_column:        ; X=offset into the title data
                    ; $72,$73=address of column top
                    ; $74=sub-row plot offset

    lda #0                  ; low/high byte flag
    sta $83

    lda #0                  ; row counter
    sta $82

    lda #$0f                ; start with red
    sta $81

    draw_column_read_next:

    ldy $83                 ; read the low/high flag
    ; load the index of the next colour change
    lda [cassette_markers + markers_length],x

    cpy #0
    bne draw_column_read_next_high

    clc
    and #$0f
    inc $83
    jmp draw_column_loop_begin

    draw_column_read_next_high:
    clc
    dec $83
    and #$f0
    lsr
    lsr
    lsr
    lsr
    inx                     ; the next value will come from the next byte

    draw_column_loop_begin:
    adc $82                 ; add the current index to the row skip
    sta $80                 ; store the next index

    draw_column_loop:

        lda $81             ; load the current colour
        ldy $74
        sta ($72),y

        lda $72             ; move to the next row on the screen
        adc #$40
        sta $72
        lda $73
        adc #$01
        sta $73
        clc

        lda $82
        cmp #$0f            ; if the last row, plot the next column
        beq draw_column_loop_next

        cmp $80             ; compare with the next index
        bne draw_column_loop_continue

        inc $82                     ; increment the row counter
        lda $81
        eor #$f0                    ; red <-> yellow
        sta $81

        jmp draw_column_read_next

        draw_column_loop_continue:

        inc $82                     ; increment the row counter
        jmp draw_column_loop

    draw_column_loop_next:

    lda #$0f
    ldy $74
    sta ($72),y

    clc
    rts

; These tables map row numbers to screen offsets.
update_table_low:  .byte $40, $80, $c0, $00, $40, $80, $c0, $00
                   .byte $40, $80, $c0, $00, $40, $80, $c0, $00

update_table_high: .byte $01, $02, $03, $05, $06, $07, $08, $0a
                   .byte $0b, $0c, $0d, $0f, $10, $11, $12, $14

title_offsets: .byte 3, 2, 2, 1, 1, 2, 2, 3, 4, 5, 6, 6, 7, 7, 6, 6, 5, 4

init_update_title:

    lda #0
    sta $78             ; last column to update

    lda #0
    sta $79             ; index of offset to use for the last column

    lda #<update_title1
    sta $7a
    lda #>update_title1
    sta $7b

    rts

update_title1:

    lda $79         ; copy the index into another register for use in the
    sta $80         ; inner loop

    lda #$88
    sta $72
    lda #$5f
    sta $73

    lda #0
    sta $84                     ; column counter

    lda #0
    sta $75

update_title2:

    ldx $80                 ; look up the offset to use
    lda title_offsets,x
    sta $74

update_column:      ; $72,$73=address of column top
                    ; $74=sub-row plot offset
                    ; $75=offset into title data

    lda #0                  ; low/high byte flag
    sta $83

    lda #0                  ; row counter
    sta $82

    lda #$0f                ; start with red
    sta $81

    lda #$00                ; plot a black line at the top of the column
    ldy $74
    dey
    sta ($72),y

    lda $8f                 ; if not running as an interrupt, jump straight
    cmp #0                  ; into the column loop
    beq update_column_loop

    lda #<update_column_loop
    sta $7a
    lda #>update_column_loop
    sta $7b

    rts

update_column_loop:

        lda $81             ; load the current colour
        ldy $74
        sta ($72),y

        ldy $83                 ; read the low/high flag
        ldx $75                 ; load the index of the next colour change
        lda [cassette_markers + markers_length],x

        cpy #0
        bne update_column_read_next_high

        clc
        and #$0f
        inc $83
        jmp update_column_skip_rows

        update_column_read_next_high:
        clc
        dec $83
        and #$f0
        lsr
        lsr
        lsr
        lsr
        inc $75                 ; the next value will come from the next byte

        update_column_skip_rows:
        clc

        tax
        adc $82                 ; update the row counter with the gap - 1
        sta $82
        inc $82                 ; add 1 to make the row skip the correct size

        lda update_table_low,x
        adc $72
        sta $72
        lda update_table_high,x
        adc $73
        sta $73
        clc

        lda $81
        ldy $74
        dey
        sta ($72),y

        lda $82
        cmp #$10            ; if the last row, plot the next column
        bcs update_column_loop_next

        lda $81
        eor #$f0                    ; red <-> yellow
        sta $81

        lda $8f
        cmp #0
        beq update_column_loop

        rts                 ; keep $7a,$7b the same (loop)

    update_column_loop_next:
    clc

    lda #$0f
    ldy $74
    sta ($72),y

    lda $72
    adc #$40
    sta $76
    lda $73
    adc #$01
    sta $77
    clc

    lda #$00                ; plot a black line at the bottom of the column
    ldy $74
    sta ($76),y

    lda #$0f                ; plot the top of the last row
    dey
    sta ($76),y

    lda $8f                 ; if not running as an interrupt, jump straight
    cmp #0                  ; into the next part of the routine
    beq update_title3

    lda #<update_title3
    sta $7a
    lda #>update_title3
    sta $7b

    clc
    rts

update_title3:

    sec
    lda $72
    sbc #$f8
    sta $72
    lda $73
    sbc #$13
    sta $73
    clc

    lda $83                         ; if we encounter a 0xff padding byte
    cmp #0                          ; then increment X to skip past it
    beq update_title_loop_no_increment

    inc $75

    update_title_loop_no_increment:

    dec $80
    bpl update_title_loop_no_index_reset

    lda #17
    sta $80

    update_title_loop_no_index_reset:
    clc

    lda $84
    cmp $78                     ; update up until the last column
    beq update_title_loop_exit
    inc $84

    lda $8f
    cmp #0
    bne update_title3_break

    jmp update_title2

    update_title3_break:

    lda #<update_title2
    sta $7a
    lda #>update_title2
    sta $7b

    clc                         ; loop back to update_title2
    rts

    update_title_loop_exit:

    lda $78
    cmp #37
    bcs update_title_outer_loop_next

    inc $78

    update_title_outer_loop_next:
    clc

    inc $79
    lda $79
    cmp #18
    bne update_title_outer_loop_no_index_reset

    lda #0
    sta $79

    update_title_outer_loop_no_index_reset:
    clc

    lda #<update_title1
    sta $7a
    lda #>update_title1
    sta $7b

    rts

update_title:

    jmp ($7a)               ; jump to the current title routine
                            ; the routine will use rts to return

cassette_original_irq1v:
cassette_original_irq1v_low: .byte 0
cassette_original_irq1v_high: .byte 0

cassette_init:

    ldx #0
    lda cassette_markers,x      ; total number of markers
    sta $7c

    inx
    ldy #0
    cassette_init_values_loop:

        lda cassette_markers,x      ; load the address of each marker
        sta $70
        inx
        lda cassette_markers,x
        sta $71
        inx
        lda cassette_markers,x      ; store the inverse value in the address
        eor #$ff
        sta ($70),y
        inx

        dec $7c
        bpl cassette_init_values_loop

    stx cassette_markers_end

    sei         ; disable interrupts

    lda $204
    sta cassette_original_irq1v_low
    lda $205
    sta cassette_original_irq1v_high

    lda #<cassette_interrupt_routine
    sta $204
    lda #>cassette_interrupt_routine
    sta $205

    cli         ; enable interrupts
    rts

cassette_quit:
    sei         ; disable interrupts

    lda cassette_original_irq1v_low
    sta $204
    lda cassette_original_irq1v_high
    sta $205

    cli         ; enable interrupts
    rts

cassette_marker_index: .byte 1
cassette_markers_end:  .byte 0
cassette_screen_index_low:  .byte 0
cassette_screen_index_high: .byte 0

.alias progress_bar_start $7614

cassette_screen_value:  .byte 1
cassette_screen_values: .byte $f0, $c0

cassette_interrupt_routine:
    pha
    txa
    pha
    tya
    pha

    ldx cassette_marker_index       ; load the current index to a marker
    cpx cassette_markers_end        ; compare it to the end index
    beq cassette_interrupt_routine_continue

    lda cassette_markers,x          ; load the address to check
    sta $70
    inx
    lda cassette_markers,x
    sta $71

    ldy #0
    lda ($70),y                     ; load the marker value
    inx
    cmp cassette_markers,x          ; compare it with the value in the table
    bne cassette_interrupt_routine_continue
    clc

    inx
    stx cassette_marker_index       ; store the new index

    lda #<progress_bar_start        ; load the position on the screen to plot to
    adc cassette_screen_index_low
    sta $70
    lda #>progress_bar_start
    adc cassette_screen_index_high
    sta $71
    clc

    ldx cassette_screen_value
    lda cassette_screen_values,x
    ldy #3
    cassette_interrupt_routine_plot_loop:

        sta ($70),y
        dey
        bpl cassette_interrupt_routine_plot_loop

    dec cassette_screen_value
    bpl cassette_interrupt_routine_continue
    clc

    lda #1
    sta cassette_screen_value
    lda cassette_screen_index_low
    adc #8
    sta cassette_screen_index_low
    lda cassette_screen_index_high
    adc #0
    sta cassette_screen_index_high

    cassette_interrupt_routine_continue:
    clc

;    jsr update_title

    pla
    tay
    pla
    tax
    pla
    jmp (cassette_original_irq1v)

cassette_markers:
