; Copyright (C) 2013 David Boddie <david@boddie.org.uk>
;
; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

show_bank1:

    jsr vsync
    lda #0              ; Use 0 and 1 for banks 1 and 2.
    ldx #$06
    jmp show_bank_common

show_bank2:

    jsr vsync
    lda #1              ; Use 0 and 1 for banks 1 and 2.
    ldx #$0b

show_bank_common:

    sta bank_number

    lda #12
    sta $fe00
    stx $fe01

    lda #13
    sta $fe00
    lda #$00
    sta $fe01

    rts

.alias palette_entries_fe21_logical_02 $80

palette_entries_fe21_entries: .byte $00, $10, $40, $50

.alias palette_entries_logical_standard_00 $07
.alias palette_entries_logical_standard_01 $26
.alias palette_entries_logical_standard_03 $a4

change_palette:         ; A=colour value (0-7)

    eor #$07                                ; Process the physical colour.
    ora #palette_entries_fe21_logical_02    ; Mix in the logical colour.
    sta $74                                 ; Store this value for later use.

    ldx #3
    change_palette_loop:

        lda palette_entries_fe21_entries,x
        ora $74
        sta $fe21
        dex
        bpl change_palette_loop

    ; Set the other standard colours.

    ldx #3
    change_palette_standard_loop:

        lda palette_entries_fe21_entries,x
        tay
        ora #palette_entries_logical_standard_00
        sta $fe21
        tya
        ora #palette_entries_logical_standard_01
        sta $fe21
        tya
        ora #palette_entries_logical_standard_03
        sta $fe21
        dex
        bpl change_palette_standard_loop

    clc
    rts
